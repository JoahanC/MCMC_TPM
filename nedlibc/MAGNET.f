	SUBROUTINE MAGNET(T,BFIELD)
C
C	GIVES MAGNETIC FIELD AT COBE IN CELESTIAL COORDINATES
C	BY NED WRIGHT 7/28/81                 
C
C	SHOULD BE CHANGED TO GIVE B IN 1950.0 COORDINATES, BUT WHY BOTHER
C
C	INPUT: T IS REAL*8 SECONDS SINCE 5/24/68
C	OUTPUT: BFIELD IS IN GAUSS
C
	REAL*8 T
	DIMENSION BFIELD(3),XYZ(3)
	DATA TWOPI/6.283185307/
C
C	OFFSET DIPOLE MODEL
C
	DIMENSION R(3),D(3),RD(3)
	DATA AMU,RD,D/7.98E10,-371.,234.,145.,-.0529878,
	1   .1904225,-.9802712/
C	COMPUTE GREENWICH SIDEREAL TIME IN RADIANS
	SIDTIM=GST(T)
	CT=COS(SIDTIM)
	ST=SIN(SIDTIM)
	CALL ORBIT(T,XYZ)
C	ROTATE TO EARTH FIXED COORDINATES AND SUBTRACT OFFSET
	R(3)=XYZ(3)-RD(3)
	R(1)=CT*XYZ(1)+ST*XYZ(2)-RD(1)
	R(2)=CT*XYZ(2)-ST*XYZ(1)-RD(2)
C	NORMALIZE
	S=R(1)**2+R(2)**2+R(3)**2
	RR=SQRT(S)
	DO 20 I=1,3
20	R(I)=R(I)/RR
	DD=DOT(R,D)*3.
	F=AMU/RR**3
C	FIELD OF A DIPOLE
	DO 30 I=1,3
30	BFIELD(I)=F*(D(I)-R(I)*DD)
C	ROTATE BACK TO CELESTIAL
	TT=CT*BFIELD(1)-ST*BFIELD(2)
	BFIELD(2)=CT*BFIELD(2)+ST*BFIELD(1)
	BFIELD(1)=TT
	RETURN
	END
