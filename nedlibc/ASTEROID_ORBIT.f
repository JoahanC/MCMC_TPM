	SUBROUTINE ASTEROID_ORBIT(NA,JDIN,RR)
	INTEGER NA
	REAL*8 JDIN
	REAL*4 RR(3)
C
C	CONVERTS ELLIPTICAL ELEMENTS IN ORB INTO RR AT JDIN
C	NA: asteroid number in 'ASTORBITS' file
C	JDIN: Julian date for position
C	RR: heliocentric current ecliptic vector position in AU
C
	PARAMETER (NAST=26)
	COMMON /ASTEROID_ORBITS/ JD(2),ND,ORB(10,2,NAST)
	REAL*8 JD
	INTEGER*4 ND
	REAL*4 ORB
C
	PARAMETER (PI=3.1415927,TWOPI=6.2831853,DTR=57.29578)
	REAL*4 R(3,2)
	DO ID=1,ND
	  A=ORB(2,ID,NA)
	  T=JDIN-JD(1)
	  AN=(T-ORB(1,ID,NA))*ORB(10,ID,NA)
	  AN = MOD(AN,TWOPI)
	  E=ORB(3,ID,NA)
	  NIT=0
	  EE=AN+E*SIN(AN)
10	    ERR=AN-EE+E*SIN(EE)
  	    IF(ABS(ERR).LT.1.E-6) GO TO 20
	    D=E*COS(EE)-1.
	    DE=-ERR/D
	    EE=EE+DE
	    NIT=NIT+1
	    IF(NIT.LT.10) GO TO 10
	    WRITE (*,900) ERR
900	FORMAT(' NOT CONVERGED IN ORBRV, ERR=',F12.7)
20	  F=1.-E*E
	  CE=COS(EE)
	  SE=SIN(EE)
	  CP=(CE-E)/(1.-E*CE)
	  SP=SE*(1.+E*CP)/SQRT(F)
	  RHO=A*F/(1.+E*CP)
	  DO 30 I=1,3
30	    R(I,ID)=RHO*(CP*ORB(I+3,ID,NA)+SP*ORB(I+6,ID,NA))
	ENDDO
	IF(ND.EQ.1) THEN
	  DO I=1,3
	    RR(I)=R(I,1)
	  ENDDO
	  GO TO 100
	ENDIF
	X=(JDIN-JD(1))/(JD(2)-JD(1))
	IF(X.LE.0.) THEN
	  V=1.
	ELSE IF (X.GE.1.) THEN
	  V=0.
	ELSE
	  V=1.-3.*X**2+2.*X**3
	ENDIF
	W=1.-V
	DO I=1,3
	  RR(I)=V*R(I,1)+W*R(I,2)
	ENDDO
	EPS=SQRT(DVS(R(1,1),R(1,2))/DOT(R,R))
	WRITE (*,9666) JDIN,NA,EPS
9666	FORMAT(F12.1,I8,F10.6)
C
C	CONVERT FROM J2000.0 TO CURRENT EPOCH, NEGLECTING THE "b" TERM
C	WHICH IS  < 7" FOR 1985-2015
C
100	T=(JDIN-2451545.D0)/36525.D0
	A=T*(1.396971+0.0003086*T)/DTR
	SA=SIN(A)
	CA=COS(A)
	T=RR(1)*CA-RR(2)*SA
	RR(2)=RR(2)*CA+RR(1)*SA
	RR(1)=T
	RETURN
	END
