	SUBROUTINE T68_TO_UTC(T,IY,MON,ID,IH,MINUTE,SECOND)
C
C	CONVERTS TIME TAG T INTO UT TIME IH:MINUTE:SECOND ON IM/ID/IY
C	T is seconds since 5/24/68 allowing for leap seconds
C
	REAL*8 T,TT,DT,DTT
	INTEGER*4 MAXDAY(12)
	REAL*8 DAY
	DATA MAXDAY/31,28,31,30,31,30,31,31,30,31,30,31/
	DAY = T/864.D2+140
	IY = DAY/365.25D0+1968
	DAY = MOD(DAY,365.25D0)
	IF (DAY.LT.0) THEN
	  DAY = DAY+365.25D0
	  IY = IY-1
	ENDIF
	MON = DAY/30.5D0+1.
	ID = MOD(DAY,30.5D0)+1.
10	MAXD = MAXDAY(MON)
C	check for Leap year Februaries:
	IF (MON.EQ.2 .AND. ((MOD(IY,4).EQ.0 .AND. MOD(IY,100).NE.0) .OR.
	1	MOD(IY,400).EQ.0) ) MAXD=29
	IF (ID.GT.MAXD) THEN
	  ID = ID-MAXD
	  MON = MON+1
	  IF (MON.GT.12) THEN
	    MON = 1
	    IY = IY+1
	  ENDIF
	  GO TO 10
	ENDIF
	CALL TIME_TAG(IY,MON,ID,0,0,1.,TT)
	DT = T-TT+1.D0
C	MAKE SURE THE DAY IS RIGHT
	IF (DT.LT.86400.D0) THEN
	  IH = DT/36.D2
	  DT = DT-36.D2*IH
	  MINUTE = DT/60.D0
	  SECOND = DT-60.D0*MINUTE
	  RETURN
	ENDIF
C	CHECK FOR A LEAP SECOND
	IF (DT.LT.86401.D0) THEN
	  CALL TIME_TAG(IY,MON,ID+1,0,0,1.,TT)
	  DTT = T-TT+1.D0
	  IF (DTT.GE.0.) THEN
	    ID = ID+1
	    GO TO 10
	  ENDIF
	  IH = 23
	  MINUTE = 59
	  SECOND = DT - 86340.D0
	  RETURN
	ENDIF
	ID = ID+DT/86401.D0
	GO TO 10
	END
