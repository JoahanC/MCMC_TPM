	SUBROUTINE DMR_PIXNO(C,NPIX)
C
C	ROUTINE TO CONVERT INPUT UNIT VECTOR C INTO A DMR PIXEL NUMBER
C
C	INPUT: REAL*4 C(3)	UNIT VECTOR
C	OUTPUT: INTEGER NPIX, RANGE 0 -> 6*32**2 -1 = 6143
C
C	MAX RADIUS = 2.378 DEGREE FOR WORST CASE IN CORNER
C	RMS PIXEL RADIUS = 1.08 DEGREES AVERAGED OVER SPHERE
C
C	TIMING: 3 MILLISECONDS ON PDP 11/34
C
	REAL*4 C(3)
	INTEGER*4 NPIX
	SAVE /DMRBIT/
	COMMON /DMRBIT/ IX(32),IY(32)
	DATA IX(32) /0/
	IF(IX(32).EQ.0) THEN
	   DO 30 I=1,32
	      J=I-1
	      K=0
	      IP=1
10	      IF(J.EQ.0) THEN
		IX(I)=K
		IY(I)=2*K
	      ELSE
		ID=MOD(J,2)
		J=J/2
		K=IP*ID+K
		IP=IP*4
		GO TO 10
	      ENDIF
30	   CONTINUE
	ENDIF
C
	CALL AXISXY(C,NFACE,X,Y)
	I=32.*X
	J=32.*Y
	I=MIN(31,I)			!Roundoff protection for cases very
	J=MIN(31,J)			!near an edge of the cube
	NPIX=NFACE*1024+IX(I+1)+IY(J+1)
	RETURN
	END
