	SUBROUTINE FCUBE(X,Y,XI,ETA)
C
C	THIS FUNCTION EVALUATES THE DIRECT TRANSFORMATION
C	FROM THE EQUAL AREA SQUARE TO THE TANGENT PLANE
C	SEE CSC REPORT ON THE QUADRILATERALIZED SPHERE
C
	REAL*4 X,Y,XI,ETA
	PARAMETER (GAMMA=0.7236012545582)
	PARAMETER (DELTA=0.7904864491208)
	PARAMETER (OMEGA=-1.225441487984)
	PARAMETER (C00=-2.7217053661814,C10=-5.5842168305430,
	1   C01=2.1711174809423,C20=-3.4578627473390,
	2   C11=-6.4160151526783,C02=1.9736265758872)
	PARAMETER (D0=1.4833129294187,D1=1.1199726069742,
	1   D2=6.0515382161464)
	PARAMETER (THIRD=0.333333333333)
	PARAMETER (THRESHOLD=0.333334)
	XX=X*X
	YY=Y*Y
	IF(AMAX1(XX,YY).GT.THRESHOLD) WRITE (*,*) ' X,Y TOO BIG=',X,Y
	RRXX=THIRD-XX
	RRYY=THIRD-YY
	XXXX=XX*XX
	YYYY=YY*YY
	XXYY=XX*YY
	XI=X*(GAMMA+(1.-GAMMA)*XX/THIRD + RRXX*(
	1  YY*(DELTA+RRYY*(C00+C10*XX+C01*YY+C20*XXXX+C11*XXYY+C02*YYYY))
	3 +XX*(OMEGA+RRXX*(D0+D1*XX+D2*XXXX)) ))
	ETA=Y*(GAMMA+(1.-GAMMA)*YY/THIRD + RRYY*(
	1 +XX*(DELTA+RRXX*(C00+C10*YY+C01*XX+C20*YYYY+C11*XXYY+C02*XXXX))
	3 +YY*(OMEGA+RRYY*(D0+D1*YY+D2*YYYY)) ))
	RETURN
	END
