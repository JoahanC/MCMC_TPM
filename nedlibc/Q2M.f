	SUBROUTINE Q2M(Q,A)
C
C	CONVERTS QUATERNION Q INTO ROTATION MATRIX A
C
	REAL*4 Q(4),A(3,3)
C
C	CONVERT TO CSC STYLE QUATERNION: I USE V(CEL)=A*V(SC)
C		BUT CSC USES V(SC)=A*V(CEL), SO USE -SPACE PARTS
C
	Q1=-Q(1)
	Q2=-Q(2)
	Q3=-Q(3)
	Q4=Q(4)
C
10	Q11=Q1*Q1
	Q22=Q2*Q2
	Q33=Q3*Q3
	Q44=Q4*Q4
	S=Q11+Q22+Q33+Q44
	IF(ABS(S-1.).GT.1.E-4) THEN
	  WRITE (*,*) ' NORMALIZATION ERROR IN NEW_Q2M'
	  S=SQRT(S)
	  Q1=Q1/S
	  Q2=Q2/S
	  Q3=Q3/S
	  Q4=Q4/S
	  GO TO 10
	ENDIF
	Q12=Q1*Q2
	Q13=Q1*Q3
	Q14=Q1*Q4
	Q23=Q2*Q3
	Q24=Q2*Q4
	Q34=Q3*Q4
C
	A(1,1) = Q11 - Q22 - Q33 + Q44
	A(1,2) = 2. * ( Q12 + Q34 )
	A(1,3) = 2. * ( Q13 - Q24 )
	A(2,1) = 2. * ( Q12 - Q34 )
	A(2,2) = -Q11 + Q22 - Q33 + Q44
	A(2,3) = 2. * ( Q23 + Q14 )
	A(3,1) = 2. * ( Q13 + Q24 )
	A(3,2) = 2. * ( Q23 - Q14 )
	A(3,3) = -Q11 - Q22 + Q33 + Q44
C
	RETURN
	END
