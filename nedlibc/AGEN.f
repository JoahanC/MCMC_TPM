	SUBROUTINE AGEN(T,A,R)
C
C	ROUTINE TO GENERATE NOMINAL COBE POINTING
C	BY NED WRIGHT 7/28/81
C	MODIFIED 8/21/81 TO ALSO RETURN MATRIX R
C	MODIFIED 18-NOV-82 TO CONFORM TO STANDARD COBE COORDINATE SYSTEM
C		WITH +X AXIS = -SPIN AXIS, CLOSE TO NADIR
C	MODIFIED 28-MAR-83 TO FOLLOW EARTH WITH LOOSE SERVO
C	MODIFIED 29-MAR-83 TO COMPUTE THE DEAD BAND BEHAVIOUR
C	MODIFIED 26-FEB-90 TO USE APPROX CORRECT SPIN RATE AND PITCH
C
C	INPUT: T IS REAL*8 SECONDS SINCE 5/24/68
C	OUTPUT: A IS A 3X3 ARRAY THAT CONVERTS COBE FIXED VECTORS
C		TO CELESTIAL COORDINATES,IE
C		B(CEL) = A*B(COBE)
C		R IS 3X3 ARRAY THAT CONVERTS COBE DESPUN VECTORS TO CELESTIAL
C
C	SUBROUTINES USED: SUNPOS TO GIVE CELESTIAL POSITION OF SUN
C			  NADIR TO GIVE CELESTIAL POSITION OF CENTER OF EARTH
C
C	METHOD: LINEAR INTERPOLATION OF SPIN AXIS OVER INTERVAL OF ONE ORBIT
C
C	IMPORTANT PARAMETERS
C		90+TILT IS THE ANGLE FROM THE SUN TO THE SPIN AXIS
C		DEAD IS THE FULL WIDTH OF THE DEAD BAND IN DEGREES
C		SIGN IS THE SIGN OF (IYY+IZZ)/2.-IXX, WHERE IXX IS THE
C			SPIN AXIS MOMENT OF INERTIA, ETC.
C		PITCH IS THE PITCH COMMAND IN DEGREES
C		PROT IS THE ROTATION PERIOD IN SECONDS
C
	REAL*8 PROT
	REAL*4 TILT,PITCH,SIGN,DEAD
	COMMON /PPARAM/ PROT,TILT,PITCH,SIGN,DEAD
C
	DIMENSION SUN(3),EARTH(3),A(3,3),R(3,3),SPIN(3),Y(3),X(3)
	DIMENSION SUN1(3),SUN2(3),EARTH1(3),EARTH2(3),EPERP(3)
	REAL*8 T,T1,T2,TRY
	PARAMETER (PI=3.1415927,TWOPI=6.2831853,DTR=57.29578)
	DATA PITCH,SIGN,DEAD,TILT,PROT,IENT,T1,T2/
	1	-6.18,1.,0.01,4.5,73.6196319D0,0,1.D0,-1.D0/
	SAVE T1,T2
	SAVE EARTH1,EARTH2,SUN1,SUN2,EPERP
C
	ALIM(DUM)=AMAX1(-1.,AMIN1(1.,DUM))
C
10	THETA=TWOPI*DMOD(T/PROT,1.D0)
C
C	CHECK FOR BRACKET: SEE IF T1 LE T LE T2
	IF (T1.LE.T .AND. T.LE.T2) GO TO 100
C
C	SEARCH FOR LAST NODE CROSSING BEFORE T
C
	T1=T
	CALL NADIR(T1,EARTH)
15	TRY=T1-1000.D0
	E3=EARTH(3)
	CALL NADIR(TRY,EARTH)
	IF(E3*EARTH(3).LE.0.) GO TO 20
	T1=TRY
	GO TO 15
20	IF(ABS(EARTH(3)).LT.1.E-4) GO TO 40
	F=EARTH(3)/(EARTH(3)-E3)
	DT=(T1-TRY)*F
	T1=TRY
	E3=EARTH(3)
	TRY=TRY+DT
	CALL NADIR(TRY,EARTH)
	GO TO 20
40	T1=TRY
C
C	FIND NODE CROSSING ONE ORBIT LATER
C
	T2=T1+5500.D0
	CALL NADIR(T2,EARTH)
45	TRY=T2+1000.D0
	E3=EARTH(3)
	CALL NADIR(TRY,EARTH)
	IF(E3*EARTH(3).LE.0.) GO TO 50
	T2=TRY
	GO TO 45
50	IF(ABS(EARTH(3)).LT.1.E-4) GO TO 70
	F=EARTH(3)/(EARTH(3)-E3)
	DT=(T2-TRY)*F
	T2=TRY
	E3=EARTH(3)
	TRY=TRY+DT
	CALL NADIR(TRY,EARTH)
	GO TO 50
70	T2=TRY
	CALL SUNPOS(T1,SUN1)
	CALL SUNPOS(T2,SUN2)
	CALL NADIR(T1,EARTH1)
	CALL NADIR(T2,EARTH2)
	CALL NADIR(T1+.25*(T2-T1),EPERP)
	E=DOT(EARTH1,EPERP)
	DO 75 I=1,3
75	EPERP(I)=EPERP(I)-E*EARTH1(I)
	CALL NORM(EPERP)
C
C	CALCULATE THE SUN-EARTH ANGLE TO KNOW THE GRAVITY GRADIENT TORQUES
C
	COSINE=DOT(SUN1,EPERP)
	SEA=ATAN2(SQRT(1.-COSINE**2),COSINE)*DTR
	A25=SEA-(90.+TILT)
	A75=(180.-SEA)-(90.+TILT)
	IF(A25*A75.LT.0.) GO TO 80
C
C	NO DEAD BAND CROSSINGS WHEN TORQUE IS ALWAYS THE SAME SIGN
C
	VC=100.
	SLOPE=0.
	GO TO 95
C
C	HERE WE WILL SHOW DEAD BAND BEHAVIOUR.  CALCULATE TIMES WHEN TORQUE
C	CROSSES ZERO
C
80	SX=-((A25+A75)/(A25-A75))
	CX=SQRT(1.-SX**2)
	XX=ATAN2(SX,CX)/TWOPI
	RATE=TWOPI*CX*(A25-A75)/2.
	DX=ABS(DEAD/RATE)
	XC=.25+DX/2.
	SLOPE=2./DX
	VC=(XC-XX)*SLOPE-1.
95	IF(A25*SIGN.LT.0.) VC=-VC
	IF(A25*SIGN.LT.0.) SLOPE = -SLOPE
C
C	CONSTRUCT POINTING BY INTERPOLATION
C
100	F=(T-T1)/(T2-T1)
	TH=TWOPI*F+PITCH/DTR
	S=SIN(TH)
	C=COS(TH)
	DO 110 I=1,3
	SUN(I)=SUN1(I)+F*(SUN2(I)-SUN1(I))
110	EARTH(I)=C*(EARTH1(I)+F*(EARTH2(I)-EARTH1(I)))+S*EPERP(I)
	SDE=DOT(SUN,EARTH)
C	CONSTRUCT VECTOR PERPENDICULAR TO SUN IN SUN EARTH PLANE
	DO 120 I=1,3
120	Y(I)=-EARTH(I)+SDE*SUN(I)
	CALL NORM(Y)
C
C	TILT SPIN AXIS BACK FROM SUN
C	FIRST COMPUTE THE CURRENT TILT
	IF(F.GT..75) F=F-1.
	TLT=TILT+ALIM(VC-SLOPE*ABS(XC-F))*DEAD/2.
	CT=COS(TLT/DTR)
	ST=SIN(TLT/DTR)
	DO 130 I=1,3
	SPIN(I)=CT*Y(I)-ST*SUN(I)
130	X(I)=CT*SUN(I)+ST*Y(I)
C	GET Y AXIS PERP TO SUN EARTH PLANE
	CALL CROSS(SPIN,X,Y)
	C=COS(THETA)
	S=SIN(THETA)
C	CONSTRUCT A BY COLUMNS
	DO 140 I=1,3
C	SETUP R MATRIX
	R(I,1)=-SPIN(I)
	R(I,3)=X(I)
	R(I,2)=Y(I)
	A(I,1)=-SPIN(I)
C	FIRST COLUMN EQUALS -SPIN AXIS
C	SECOND TWO COLUMNS ROTATE IN X-Y PLANE
	A(I,3)=C*X(I)+S*Y(I)
140	A(I,2)=C*Y(I)-S*X(I)
	RETURN
	END
