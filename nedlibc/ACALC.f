	SUBROUTINE ACALC(AZ,EL,TWST,ALIGN)
C
C	COMPUTES ALIGN(3,3), THE ROTATION MATRIX FROM SC ->
C	CONTROL AXIS COORDINATES, SO V(CA) = ALIGN*V(SC)
C	INPUT AZ,EL,TWST IN DEGREES
C	IN CONTROL AXIS COORDINATES, +Z AXIS IS SUN SENSOR OPTICAL AXIS
C	+X AXIS -> SC +X AXIS
C
C	NOTE THAT NOMINAL ALIGNMENT IS AZ=0,120,240 FOR A,B,C
C	AND EL=0, TWST=0 FOR A,B,C
C
C	Tested 8-Sep-83 using M2Q -> ALIGN is always a rotation matrix
C
	REAL*4 AZ,EL,TWST,ALIGN(3,3)
	PARAMETER (PI=3.1415927,TWOPI=6.2831853,DTR=57.29578)
	CA=COS(AZ/DTR)
	SA=SIN(AZ/DTR)		!changed back 5-Jun-85
C
C	based on ACS COMPONENT LAYOUT vugraph in PDR book (Nov 7, 1984)
C	the B axis is close to -Y axis
C
	CE=COS(EL/DTR)
	SE=SIN(EL/DTR)
	CT=COS(TWST/DTR)
	ST=SIN(TWST/DTR)
	ALIGN(1,1)=CE*CT
	ALIGN(2,1)=-ST*CE
	ALIGN(1,2)=-SE*SA*CT+ST*CA
	ALIGN(2,2)=CA*CT+ST*SE*SA
	ALIGN(1,3)=CT*SE*CA+ST*SA
	ALIGN(2,3)=CT*SA-ST*SE*CA
	ALIGN(3,1)=-SE
	ALIGN(3,2)=-CE*SA
	ALIGN(3,3)=CE*CA
	END
