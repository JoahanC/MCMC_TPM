	SUBROUTINE EPHEM(N,T,P)
C
C	EPHEMERIS ROUTINE FOR COBE
C	INPUT N INTEGER*4 PLANETS NUMBER
C		-1 FOR SUN
C		0 FOR MOON
C		1 FOR MARS
C		2 FOR JUPITER
C		3 FOR SATURN
C		4 FOR URANUS
C		5 FOR NEPTUNE
C		6 FOR 1st asteroid IN FILE 'ASTORBITS', 7 FOR 2nd etc.
C
C	INPUT T REAL*8 TIME SINCE 5/24/68 0 HOURS UTC
C
C	OUTPUT VECTOR P, REAL*4 P(3)
C		DIRECTION OF P IS GEOCENTRIC POSITION OF PLANET
C		IN CELESTIAL COORDINATE SYSTEM WITH EPOCH 2000
C		MAGNITUDE IS DISTANCE IN AU EXCEPT FOR MOON IN EARTH RADII
C
C	Based on Van Flandern & Pulkinnen, 1979, Ap.J.Supp. v. 41, pp. 391-411
C	Only the heliocentric series for PLON, BETA and RP are used.
C	The U, V, W series have been deleted for compactness.
C
	INTEGER*4 N
	REAL*4 P(3),PE(3),SE(3),CONST(33),DBAR(10),STR
	REAL*4 PRECES(3,3) /9*0./
	REAL*8 T,TT,TTT,BETA,RP,PLON,ARGS(33),U,V,W,SLOPES(33)
	REAL*8 BETASUN
	REAL*8 JULIAN_DAY,DJD,CYCLES
	REAL*8 JD /24400000.D0/
	REAL*8 TOLD/-1.D20/
	COMMON /FMCMMN/ PLON,U,V,W,ARGS
	PARAMETER (PI=3.1415927,TWOPI=6.2831853,DTR=57.29578)
	PARAMETER (STR=3600*DTR)
	PARAMETER (CCC = 173.144633) ! speed of light in AU/day
	DATA CONST/.606434,.374897,.259091,.827362,.347343,0.,
	1   .779072,.993126,.700695,.485541,.566441,.505498,
	2   .140023,.292498,.987353,.053856,.849694,.089608,
	3   .056531,.814794,.133295,.882987,.821218,.870169,
	4   .400589,.664614,.846912,.725368,.480856,0.,
	5   .663854,.041020,.357335/
	DATA SLOPES/.3660110129D-1,.3629164709D-1,.367481952D-1,
	1   .3386319198D-1,-.14709391D-3,0.D0,		!Moon
	2   .273790931D-2,.27377785D-2,			!Sun
	3   .11367714D-1,.1136759566D-1,.1136762384D-1,	!Mercury
	4   .445046867D-2,.445036173D-2,.445040017D-2,	!Venus
	5   .145575328D-2,.145561327D-2,.145569465D-2,	!Mars
	6   3*.23080893D-3,				!Jupiter???
	7   3*.9294371D-4,				!Saturn???
	8   2*.3269438D-4,.3265562D-4,			!Uranus
	9   2*.1672092D-4,.1663715D-4,	!Neptune fixed 9/21/92
	9   0.D0,.1115482D-4,2*.1104864D-4/		!Pluto
	DATA DBAR/1.00021,60.40974,1.83094,	!Sun, Moon, Mars
	1   5.30693,9.61711,19.24877,30.08900,	!Jupiter,Saturn,Uranus,Neptune
	2   41.3268,1.07693,1.23437/		!Pluto,Mercury,Venus
C
C
C	COMPUTE ARGUMENTS
C
	SAVE JD,TT,TTT,TOLD,PRECES
	IF(DABS(T-TOLD).LE.1.E-2) GO TO 150
	  JD=JULIAN_DAY(T)
	  TT=JD-2451545.D0			!Days since J2000.0
	  TTT=TT/36525.D0+1.D0			!Centuries since 1900.0
	  EPOCH=1900.0+100.0*TTT
	  CALL PREMAT(EPOCH,PRECES)
	  DO I=1,33
	    CYCLES=CONST(I)+SLOPES(I)*TT
	    ARGS(I)=TWOPI*DMOD(CYCLES,1.D0)
	  ENDDO
	  TOLD=T
C	
C	CALL APPROPRIATE ROUTINE
C
150	DO 152 I=1,3
	   PE(I)=0.				
152	   SE(I)=0.
	IF (N.NE.0) THEN
	   CALL SUN(BETASUN,RP,TTT)		
	   SUNLON=ARGS(7)+PLON/STR		
	   SE(1)=COS(SUNLON)*RP
	   SE(2)=SIN(SUNLON)*RP
	ENDIF
	IF (N.EQ.-1) GO TO 200			
	IF (N.EQ.0) THEN
	   CALL MOONVF(BETA,RP,TTT)
	   NL=1
	ENDIF
	IF (N.EQ.1) THEN
	   CALL MARS(BETA,RP,TTT)
	   NL=15
	ENDIF
	IF (N.EQ.2) THEN
	   CALL JUPITER(BETA,RP,TTT)
	   NL=18
	ENDIF
	IF (N.EQ.3) THEN
	   CALL SATURN(BETA,RP,TTT)
	   NL=21
	ENDIF
	IF (N.EQ.4) THEN
	   CALL URANUS(BETA,RP,TTT)
	   NL=24
	ENDIF
	IF (N.EQ.5) THEN
	   CALL NEPTUNE(BETA,RP,TTT)
	   NL=27
	ENDIF
	IF (N.GE.6) THEN
	   DJD=2./CCC
	   DO ITER=1,2
	      CALL ASTEROID_ORBIT(N-5,JD-DJD,PE)
	      DO I=1,3
	         PE(I)=PE(I)+SE(I)
	      ENDDO
	      DJD=SQRT(DOT(PE,PE))/CCC
	   ENDDO
	   GO TO 250
	ENDIF
	IF(N.LT.-1) STOP 'N TOO LOW IN EPHEM'
C
	PLONG=ARGS(NL)+PLON/STR
	BRAD=BETA/STR
C
C	Compute heliocentric vector in ecliptic coordinates
C
	PE(3)=RP*SIN(BRAD)			
	PE(2)=RP*COS(BRAD)			
	PE(1)=PE(2)*COS(PLONG)
	PE(2)=PE(2)*SIN(PLONG)
200	DO 220 I=1,3
220	   PE(I)=PE(I)+SE(I)			!add in Sun to get geocentric
250	EPS=(84428.-47.*TTT+9.*COS(ARGS(5)))/STR
	CEPS=COS(EPS)
	SEPS=SIN(EPS)
	DLAM=-17.*SIN(ARGS(5))/STR	!accurate conversion to celestial
	P(1)=PE(1)-DLAM*PE(2)
	PE(2)=PE(2)+DLAM*PE(1)
	P(2)=PE(2)*CEPS-PE(3)*SEPS
	P(3)=PE(3)*CEPS+PE(2)*SEPS
C
C	CONVERT TO 2000
C
	CALL VMAT(PRECES,P,PE)
	DO I=1,3
	  P(I)=P(I)+PE(I)
	ENDDO
	RETURN
	END
