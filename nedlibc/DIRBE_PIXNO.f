	SUBROUTINE DIRBE_PIXNO(C,NPIX)
C
C	CONVERTED TO 0-BASED PIXELS 6-FEB-1989
C
C
C	ROUTINE TO DEFINE DIRBE PIXELS BASED ON THE
C	QUADRILATERALIZED SPHERICAL CUBE
C
C	INPUT: REAL*4 C(3)	A unit vector specifying the LOS
C	OUTPUT: INTEGER*4 NPIX	The pixel number: 0 -> 6*256**2-1
C
C	EACH OF 6 FACES OF THE CUBE IS DIVIDED INTO 256 BY 256 PIXELS
C	THE MAX RADIUS OF A PIXEL IS 0.300 DEGREES FOR THE LONG DIAGONAL
C	AT A CORNER.
C	THE RMS RADIUS IS 0.134 DEGREES, AVERAGED OVER THE SPHERE
C
C	TIMING: 3 MILLISECONDS ON A PDP 11/34 WITH FPP
C
	INTEGER*4 NPIX,IX,IY,K,IP
	SAVE /DRBBIT/
	COMMON /DRBBIT/ IX(256),IY(256)
	DATA IX(256) /0/
	IF(IX(256).EQ.0) THEN			!Set up the lookup tables
	   DO 30 I=1,256			!for converting x,y into
	      J=I-1				!pixel numbers
	      K=0
	      IP=1
10	      IF(J.EQ.0) THEN
		IX(I)=K
		IY(I)=2*K
	      ELSE
		ID=MOD(J,2)
		J=J/2
		K=IP*ID+K
		IP=IP*4
		GO TO 10
	      ENDIF
30	   CONTINUE
	ENDIF
C
	CALL AXISXY(C,NFACE,X,Y)
	I=256.*X
	J=256.*Y
	I=MIN(255,I)			!Roundoff protection for cases very
	J=MIN(255,J)			!near an edge of the cube
	NPIX=NFACE*65536+IX(I+1)+IY(J+1)
	RETURN
	END
