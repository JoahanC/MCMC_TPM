	SUBROUTINE HORZON(T,A,NSCAN,PSI,DPSI)
C
C	BY NED WRIGHT 11-AUG-81
C	MODIFIED 18-NOV-82 TO USE COBE STANDARD COORDINATE SYSTEM
C
C......................................................................
C	EXTENSIVELY
C	MODIFIED 27-JUN-83 TO USE THREE SCANNERS AT 90, 210, AND 330
C	DEGREES AZIMUTH, WITH CONE ANGLE = 90 DEGREES
C
C	GIVEN T REAL*8 SECONDS SINCE 5/24/68 AND A(3,3) ROTATION
C	MATRIX COMPUTES SPLIT TO INDEX PSI OF NSCAN'TH SCANNER
C	SUCH THAT PSI=0 MEANS EARTH IS CENTERED, PSI=PI MEANS
C	COBE IS UPSIDE DOWN!
C	FOR THE I'TH SCANNER, MSC(,I) IS VECTOR POINTING IN THE PSI=0
C	DIRECTION;  NSC(,I) IS A VECTOR IN THE PSI=PI/2 DIRECTION
C
C	DPSI(3) IS GRAD PSI WRT CORRECTION ANGLES DELTA 1-3
C
	REAL*8 T
	REAL ESC(3),EARTH(3),DPSI(3),A(3,3),NSC(3,3),DPS(3),
	1   GRAD(3,3),MSC(3,3)
C
C	SCANNER CONE ANGLE IS 90 DEGREES AND CENTER OF SCAN IS
C	ON SPACECRAFT EQUATOR
C
C	PUT ALL SCANNERS EXACTLY ON ALIGNMENT
	DATA NSC/0.,.5,-.8660254,	!SCANNER A -> NSCAN=1
	1	 0.,.5,.8660254,	!SCANNER B -> NSCAN=2
	2	 0.,-1.,0./		!SCANNER C -> NSCAN=3
	DATA MSC/1.,0.,0.,1.,0.,0.,1.,0.,0./
C
	PARAMETER (PI=3.1415927,TWOPI=6.2831853,DTR=57.29578)
C
	CALL NADIR(T,EARTH)
	CALL VMAT(A,EARTH,ESC)
	D=DOT(ESC,NSC(1,NSCAN))
	E=DOT(ESC,MSC(1,NSCAN))
	PSI=ATAN2(D,E)
C
C	NOW COMPUTE D(PSI)/D(ESC) -> DPS
	RR=D*D+E*E
	DO 10 I=1,3
10	DPS(I)=(E*NSC(I,NSCAN)-D*MSC(I,NSCAN))/RR
C
C	GRAD IS A 3X3 MATRIX, THE GRADIENT OF ESC WRT THE DELTA 1,2,3
C	SO GRAD(I,J) IS D(ESC(I))/D(DELTA(J))
C
	CALL GENGRD(ESC,GRAD)
C
	CALL VMAT(GRAD,DPS,DPSI)
	RETURN
	END
