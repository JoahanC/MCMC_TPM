	SUBROUTINE DIRBE_CENPIX(NDIRBE,C)
C
C	center of DIRBE pixel # NDIRBE -> unit vector C
C
	INTEGER*4 NDIRBE
	REAL*4 C(3)				!Output unit vector
	INTEGER*4 JPIX,N,M,JX,JY
	REAL*4 X,Y
C
	INTEGER*2 IX,IY
	COMMON /COBE_SPRCNPX/ IX(0:1023),IY(0:1023)
	IF (IX(1023).NE.31) CALL COBE_SPRCNPX_SET
	IF (NDIRBE.LT.0 .OR. NDIRBE.GT.393215) THEN
	  WRITE (*,*) 'BAD PIXEL NUMBER IN DIRBE_CENPIX:',NDIRBE
	ENDIF
C
C	use integer arithmetic to breakup pixel number
C
	NFACE=NDIRBE/65536
	JPIX = NDIRBE-NFACE*65536
	M = JPIX/256
	N = JPIX-256*M
	JX=16*IX(M)+IX(N)
	JY=16*IY(M)+IY(N)
C
	X=(JX-127.5)/128.			!Database coordinates.  Range
	Y=(JY-127.5)/128.			! -1 -> 1 covers the square face
	CALL FORWARD_CUBE(X,Y,XI,ETA)
	CALL XYAXIS(NFACE,XI,ETA,C)
	RETURN
	END
