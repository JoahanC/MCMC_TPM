	SUBROUTINE ORBIT(TIN,XYZ)
C
C	CRUDE VERSION FOR TESTS, GIVES PURE PRECESSING CIRCULAR ORBIT
C	THIS IS SETUP FOR A DAWN LAUNCH FROM VANDENBERG, SO ECLIPSES
C		OCCUR NEAR SUMMER SOLSTICE
C	BY NED WRIGHT 7/28/81
C	MODIFIED FOR DAWN LAUNCH 2/26/90
C	INPUT: TIN IS REAL*8 SECONDS SINCE 5/24/68
C	OUTPUT: XYZ IS POSITION IN KM WRT CENTER OF EARTH
C		XYZ(1) IS TOWARD RA=0, DEC=0
C		XYZ(2) IS TOWARD RA=6H, DEC=0
C		XYZ(3) IS TOWARD DEC=90
C	CURRENT OF DATE COORDINATES, NOT J2000!!!
C
	PARAMETER (PI=3.1415927,TWOPI=6.2831853,DTR=57.29578)
	DIMENSION XYZ(3)
	REAL*8 T,TIN,OP,WORB,T_AN
	DATA IENT,PORB,RORB,RINC/0,102.8647,7271.,1.7282/
	DATA T_AN/680888544.405D0/
	DATA C,S,WORB/0.,0.,0./
	IF(IENT.EQ.0) THEN
	  IENT=1
	  C=COS(RINC)
	  S=SIN(RINC)
	  WORB=TWOPI/60./PORB
	ENDIF
	T=TIN-T_AN
	OP=T*WORB
	XYZ(1)=RORB*DCOS(OP)
	XYZ(2)=RORB*DSIN(OP)
C	INCLINE THE ORBIT
       	XYZ(3)=S*XYZ(2)
	XYZ(2)=C*XYZ(2)
C	PRECESS THE ORBIT
	PH= 0.03286 + T/5011025.9
	CS=COS(PH)
	SS=SIN(PH)
	TT=CS*XYZ(1)-SS*XYZ(2)
	XYZ(2)=CS*XYZ(2)+SS*XYZ(1)
	XYZ(1)=TT
	RETURN
	END
